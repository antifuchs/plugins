function usage {
  cat <<'EOF'
USAGE: $0 [-r|--remove] file [file ...]
EOF
}

(( $+1 )) || {
  usage
  return 1
}

[ "$1" = "-h" -o "$1" = "--help" ] && {
  usage
  return 0
}

local remove_archive=0
if [ "$1" = '-r' -o "$1" = '--remove' ]; then
  remove_archive=1
  shift
fi

(( $+1 )) || {
  usage
  return 1
}

local -a extensions
local success
local archive

zstyle -a ':zoppo:plugins:archive:extract' extensions extensions
extensions=(${(@u)extensions})
sort_by_length extensions

for archive ("$argv[@]"); do
  if [[ ! -s "$archive" ]]; then
    print "$0: file not valid: $archive" >&2
    continue
  fi

  success=1

  local ext
  for ext ("$extensions[@]"); do
    if [[ "${archive[-$(($#ext + 1)),-1]}" == ".$ext" ]]; then
      "+extract.$ext" "$archive"
      local ec="$?"

      if (( ! $ec )) && (( $remove_archive )); then
        rm "$archive"
        success=0
      fi
      break
    fi
  done

  if (( ! $success )); then
    print "$0: cannot extract: $archive" >&2
  fi
done

# vim: ft=zsh sts=2 ts=2 sw=2 et fdm=marker fmr={{{,}}}
