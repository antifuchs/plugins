(( $+2 )) || {
  warn 'archive:extensions: not enough arguments'
  return 1
}


if [[ "$2" = extensions ]]; then
  typeset -ga extensions
else
  local -a extensions
fi

local -a disable
local name

extensions=()

for name ("${(@k)functions}"); do
  [[ "$name" == archive:$1:* ]] || continue

  zstyle -a ':zoppo:plugin:archive' disable disable
  [[ "${disable[(I)${name#archive:$1:}]}" == 0 ]] || continue

  zstyle -a ":zoppo:plugin:archive:$1" disable disable
  [[ "${disable[(I)${name#archive:$1:}]}" == 0 ]] || continue

  extensions+=("${name#archive:$1:}")
done

sort_by_length extensions

[[ "$2" != extensions ]] && \
  set -A "$2" "${extensions[@]}"

# vim: ft=zsh sts=2 ts=2 sw=2 et fdm=marker fmr={{{,}}}
